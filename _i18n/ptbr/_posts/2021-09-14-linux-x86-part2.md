---
title: "[linux-x86] Aceitando uma conexão TCP"
date: 2020-09-14T12:33:00-03:00
author: Lucas Teske
layout: page
permalink: /webserver/linux/part2
tags:
  - x86
  - intel
  - amd
  - syscall
  - tcp
  - exit
---


Na [segunda parte](/webserver/linux/part1) conseguimos finalizar nosso Hello World, e até mesmo fazer uma função para escrever qualquer texto no terminal. Isso será de extrema utilidade para "debugar" as próximas partes. Obviamente poderiamos usar o [gdb](https://www.gnu.org/software/gdb/) para isso, mas que programador usa debugger ao invés de `print("chegou aqui")`?

Para termos um servidor web funcional, precisaremos dos seguintes passos:

1. Criar um `socket`
2. Escutar na porta TCP 8000
3. Aguardar uma conexão
4. Responder ao request HTTP

Vamos então colocar a mão na massa para aceitar uma conexão `TCP`!

## Aceitando uma conexão TCP

Para nosso objetivo final de ter um webserver, precisamos primeiro conseguir aceitar uma conexão [TCP](https://pt.wikipedia.org/wiki/Transmission_Control_Protocol). A conexão TCP é a base para a comunicação [HTTP](https://developer.mozilla.org/pt-BR/docs/Web/HTTP) usada no webserver.

O primeiro passo é criar um socket e para isso usaremos a `syscall socket`. Para usarmos a `syscall socket` precisaremos de três argumentos: `family`, `type` e `protocol`.

* `family` => `AF_INET(2)` - Socket de Internet (IP)
* `type` => `SOCK_STREAM(1)` - Socket TCP (Socket Stream)
* `protocol` => 0 - Não usado aqui

Para facilitar a organização, criaremos agora um arquivo `socket.asm` para colocar as funções relacionadas a `socket` e apenas chamaremos ela no nosso `helloworld.asm`, onde temos nossa função principal. Além disso iremos mover nossas funções `imprime`, `proxchar` e `chamawrite` para um arquivo `strings.asm`.


Arquivo `strings.asm`

```nasm
section .text
global imprime        ; Declaramos como global, para podermos usar em outros arquivos

imprime:
  push rdi            ; Salva RDI na stack, vamos alterar ele
  push rsi            ;

  mov rdx, 0          ; Inicializa RDX com 0
  mov rsi, rdi        ; O primeiro argumento pela convenção é o RDI, 
                      ; porém precisamos do valor em RSI para chamada lodsb

proxchar:
  lodsb               ; Carrega primeiro caracter
  cmp al, 0           ; Compara AL com 0
  je chamawrite       ; Caso AL == 0, temos o final da nossa mensagem. Salta para chamawrite
  inc rdx             ; Caso não, incremente RDX e volte para proxchar
  jmp proxchar

chamawrite:
  mov rsi, rdi        ; RSI foi alterado pelas chamadas lodsb, então vamos restaurar ovalor dela aqui
  mov rax, 1          ; 1 == Syscall write
  mov rdi, 1          ; 1 == stdout
  syscall             ; Chamar o sistema operacional

  pop rsi             ; Restaura RSI
  pop rdi             ; Restaura RDI
  ret
```

Arquivo `helloworld.asm`

```nasm
section .text
global _start

extern imprime        ; Referencia externa a função imprime

_start:
  mov rdi, helloworld
  call imprime
  mov rdi, qqeutofazendo
  call imprime
  mov rax, 60         ; 60 == Syscall Exit
  mov rdi, 0          ; 0 == OK
  syscall             ; Chamar o sistema operacional
                      ; Nunca vai voltar pra cá

section .rodata

helloworld:
  db `Ola, mundo!\n`, 0
qqeutofazendo:
  db `QQ eu to fazendo aqui?\n`, 0
```

Desta maneira podermos nos organizar melhor ao invés de ficar com um arquivo gigantesco! Repare que tivemos que mudar um pouco: declaramos a função `imprime` como `global` e no arquivo `helloworld.asm` escrevemos uma nova diretiva, a `extern`. A diretiva `extern` serve para indicar que teremos algo durante tempo de ligação e não neste arquivo, e que é para o compilador aceitar que este símbolo é externo a esse arquivo. A diretiva `global` no arquivo `strings.asm` indica para o compilador que esse símbolo será acessado por fora também, e que é para ele preservar o nome.

#### Criando o socket

A chamada de sistema `socket` irá retornar um número que representa um arquivo (similar ao `stdin`, `stdout` e `stderr`). Caso esse valor seja zero, algum erro ocorreu ao inicializar o socket. Caso ele seja maior que zero, temos um socket válido criado. 

!> Embora seja raro essa chamada falhar, é uma boa prática validar erros

```nasm
section .text
global criarsocket

; Constantes para syscall socket
AF_INET           equ 2
SOCK_STREAM       equ 1

; Numero das syscalls
SYSCALL_SOCKET    equ 41

criarsocket:
  mov rdi, AF_INET        ; IP
  mov rsi, SOCK_STREAM    ; TCP
  mov rdx, 0              ; Nao usado
  mov rax, SYSCALL_SOCKET ; Syscall socket
  syscall                 ; Chama syscall
  ret                     ; Retorno da syscall estará em RAX

```

Aqui inserimos uma nova diretiva, a `equ`. A `equ` serve para declarar valores constantes / aliases a valores. Usaremos ele para facilitar o entedimento das chamadas. Por exemplo, todas as vezes que escrevermos `AF_INET`, estamos dizendo `2`.

Em nosso `helloworld.asm` iremos chamar `criarsocket`, testar para ver se temos um socket válido, e armazenar o valor dele numa variável.


```nasm
section .text
global _start

extern imprime        ; Referencia externa a função imprime
extern criarsocket    ; Referência externa a função criarsocket

_start:
  mov rdi, helloworld
  call imprime
  mov rdi, qqeutofazendo
  call imprime

  call criarsocket    ; Chama criar socket
  cmp rax, 0          ; Verifica se rax == 0
  jz errosocket       ; Caso seja zero, mostre a mensagem de erro
  mov [meusocket], rax; Caso não, salve o valor

  jmp sair            ; Ir para final do aplicativo

errosocket:
  mov rdi, msgerrosocket
  call imprime

sair:
  mov rax, 60         ; 60 == Syscall Exit
  mov rdi, 0          ; 0 == OK
  syscall             ; Chamar o sistema operacional
                      ; Nunca vai voltar pra cá

section .data         ; Seção Leitura / Escrita

meusocket:
  dq 0                ; Usado para armazenar o socket, número 64 bits

section .rodata       ; Seção somente leitura

helloworld:
  db `Ola, mundo!\n`, 0
qqeutofazendo:
  db `QQ eu to fazendo aqui?\n`, 0
msgerrosocket:
  db `Houve um erro ao criar o socket!\n`, 0
```

Após a criação do socket, temos que configurar o socket e criar o vínculo com a porta selecionada. Para isso iremos usar as chamadas de sistema `setsockopt` e `bind`.

#### Configurando Socket

No arquivo `socket.asm`:

```nasm
section .text
global criarsocket

; Constantes para syscall socket
AF_INET             equ 2
SOCK_STREAM         equ 1

; Constantes para setsockopt
SOL_SOCKET          equ 1
SO_REUSEADDR        equ 2
SO_REUSEPORT        equ 15

; Numero das syscalls
SYSCALL_SOCKET      equ 41
SYSCALL_BIND        equ 49
SYSCALL_SETSOCKOPT  equ 54

criarsocket:
  mov rdi, AF_INET        ; IP
  mov rsi, SOCK_STREAM    ; TCP
  mov rdx, 0              ; Nao usado
  mov rax, SYSCALL_SOCKET ; Syscall socket
  syscall                 ; Chama syscall
  ret                     ; Retorno da syscall estará em RAX


configurarsocket:
                              ; Assume o socket a ser configurado em RDI
                              ; Configurar socket para escutar na porta 8081 forçadamente
  mov rsi, SOL_SOCKET         ; Estamos alterando um socket 
  mov rdx, SO_REUSEADDR       ; Flag que vamos configurar. Marcando para reutilizar o endereço, 
                              ; evita alguns problemas no restart do aplicativo
  mov r10, opttrue            ; Valor para setar a flag para true (número 1).
                              ; Este campo é um ponteiro para um numero inteiro 32 bits
  mov r8, [opttruelen]        ; Tamanho do argumento opttrue, ele espera um ponteiro para uma varíavel de 32 bits contendo o tamanho
  mov rax, SYSCALL_SETSOCKOPT ; Syscall setsockopt
  syscall                     ; Chama syscall
  ret                         ; Resultado da alteração fica em RAX

section .data

opttrue:    ; Usado na configuração, representa true / ativo
  dd 1
opttruelen: ; Tamanho do opttrue, 4 bytes
  dd 4      

```

Essa chamada de sistema pode ter alguns argumentos passado de maneira estranha, porém ela é feita desta maneira pois permite configurar praticamente qualquer flag de qualquer socket. Por isso, para ativarmos a flag `SO_REUSEADDR` passamos um endereço de um número inteiro (1, que representa true / ativo) e o tamanho da variável (nesse caso o 1 é representado por um número 32 bits) como argumentos.


### Bind da porta / endereço

Para fazer o vínculo da porta e endereço escolhidos, teremos que chamar a `syscall bind`. Porém essa chamada tem alguns argumentos que são estruturas. E como vamos lidar com estruturas no assembly?

Existem algumas maneiras de fazer isso. Uma delas é como você já deve ter imaginado, reservar o espaço e colocar os endereços na mão. Porém isso pode ficar um pouco complicado dependendo do tamanho da estrutura. Para facilitar, iremos usar a diretiva `struc`:

```nasm
struc nome_da_estrutura
  nome_campo tipo_base tamanho_em_tipo_base
endstruc
```

Onde podemos ter por exemplo:

```nasm
section .data:

struc minha_struct
  .nome   resb 24 ; Campo nome de 24 bytes
  .idade  resd 1  ; Campo idade, 1 numero inteiro de 32 bits, ou 4 bytes
endstruc

meu_objeto:
  resb minha_struct_size ; O compilador gera o simbolo minha_struct_size para nós

section .text
  mov eax, 29                 ; Coloca 29 em EAX (32 bits)
  mov [meu_objeto.idade], eax ; Coloca EAX (32 bits) em meu_objeto.idade
```

E assim será possível acessar o _endereço_ das variáveis mencionando `label.campo`. Muito mais fácil do que ficar calculando os tamanhos na mão, não é?


Voltando pra chamada de sistema `bind`, temos os seguintes argumentos:

* `rdi` => `fd` ou número do socket
* `rsi` => `umyaddr`, endereço para vincular. Este campo é um endereço para uma estrutura `struct sockaddr __user *`
* `rdx` => `addrlen` tamanho da variável passada em `rsi` (campo `umyaddr`).

A declaração da estrutura `struct sockaddr __user *` é um pouco complicado de achar no mar de código fonte do kernel, e ela tem muito mais campos do que realmente são nescessários para esta chamada de sistema (e por isso podemos passar um `addrlen` para limitar o quanto o kernel vai ler). Para nosso uso, o que está nas [páginas do man](https://man7.org/linux/man-pages/man7/ip.7.html) será suficiente. Então para simplificar irei escrever a definição em código C dela aqui:

```c
struct sockaddr_in {
  sa_family_t    sin_family; /* AF_INET */
  in_port_t      sin_port;   /* porta em ordenamento big-endian */
  struct in_addr sin_addr;   /* Endereço IP / Porta */
};

struct in_addr {              // Endereço IPv4
   uint32_t       s_addr;     /* endereço em big-endian */
};
```

Repare que o terceiro campo é outra estrutura, porém como estruturas declaram o conteúdo, podemos reescrever a estrutura da seguinte maneira:

```
struct sockaddr_in {
  sa_family_t    sin_family; /* AF_INET */
  in_port_t      sin_port;   /* porta em ordenamento big-endian */
  uint32_t       s_addr;     /* endereço em big-endian */
};
```

Vale notar que isso só é possível neste caso especifico, pois a estrutura está com os campos anteriores alinhados em 4 bytes (tanto campo `sin_family` quanto `sin_port` são campos de 2 bytes) e isso não faz o kernel esperar nenhum alinhamento de memória. Alinhamento de memória está fora do escopo deste tutorial, porém é importante saber que nem sempre é possível fazer essa substituição desta maneira.

Assumindo então que os tipos `sa_family_t` e `in_port_t` são de 2 bytes, podemos construir nossa declaração no arquivo `socket.asm` logo após as definições `equ`:

```nasm
(...)
; Numero das syscalls
SYSCALL_SOCKET      equ 41
SYSCALL_BIND        equ 49
SYSCALL_SETSOCKOPT  equ 54

struc minha_struct
  .sin_family resb 2 ; No nosso caso, sempre AF_INET
  .sin_port   resb 2 ; Porta, porém em big-endian (bytes trocados de ordem)
  .s_addr     resb 4 ; Endereço IP, porém em big-endian (bytes trocados de ordem)
endstruc

(...)
```

